buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

allprojects {
    apply plugin: "eclipse"

    group = "com.gempukku.libgdx.gdx-graph"
    version = "$gdxGraphVersion"
    ext {
        appName = "Gdx Graph"
    }

    ext.texturePacker = [
            "$projectDir/atlas/gdx-graph-skin", "$projectDir/assets/skin/gdx-graph", "uiskin"
    ]

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}

configure(subprojects) {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    sourceSets.main.java.srcDirs = ["src/"]
    sourceSets.main.resources.srcDirs = ["resources/"]
    sourceSets.test.java.srcDirs = ["test/"]
    sourceSets.test.resources.srcDirs = ["test-resources/"]

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}