apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "maven-publish"

sourceCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["resources/"]

archivesBaseName = "gdx-graph"

eclipse.project.name = "gdx-graph"

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "gdx-graph"
                description = "gdx-graph runtime library for pipeline rendering"
                url = "https://github.com/MarcinSc/gdx-graph"
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "https://mit-license.org/"
                    }
                }
                developers {
                    developer {
                        id = "MarcinSc"
                        name = "Marcin Sciesinski"
                        email = "marcins78@gmail.com"
                    }
                }
                scm {
                    connection = "git@github.com:MarcinSc/gdx-graph.git"
                    developerConnection = "git@github.com:MarcinSc/gdx-graph.git"
                    url = "https://github.com/MarcinSc/gdx-graph"
                }
            }

            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications
}
